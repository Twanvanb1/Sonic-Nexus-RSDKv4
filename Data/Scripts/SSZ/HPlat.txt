// ----------------------------------
// RSDK Project:          Sonic Nexus v4
// Script Description:    SSZ Horizontal Platform
// Script Author:         Twanvanb1
// ----------------------------------

// ========================
// Aliases
// ========================

private alias 27 : TYPE_HPLAT
private alias Object.Value0 : Object.Pressed
private alias Object.Value1 : Object.PlatformY
private alias Object.Value2 : Object.ChangeY

private alias Object.Value3 : Object.PlatformX
private alias Object.Value4 : Object.ChangeX
private alias Object.Value5 : Object.Angle

// ========================
// Events
// ========================

event ObjectUpdate
	
	Object.Angle = Oscillation
	Object.Angle <<= 1

	if Object.PropertyValue == 1
		Object.Angle += 256
	end if

	Cos(Object.ChangeX, Object.Angle)
	Object.ChangeX <<= 13
	Object.ChangeX += Object.XPos
	Object.ChangeX &= 4294901760
	Object.ChangeX -= Object.PlatformX
		

	if Object.Pressed == true
		if Object.PlatformY < 16
			Object.PlatformY++
			temp0 = Object.PlatformY
			temp0 &= 3
			if temp0 == 3
				Object.ChangeY = 65536
			else
				Object.ChangeY = 0
			end if
		else
			Object.ChangeY = 0
		end if
	else
		if Object.PlatformY>0
			Object.PlatformY--
			temp0 = Object.PlatformY
			temp0 &= 3
			if temp0 == 3
				Object.ChangeY = -65536
			else
				Object.ChangeY = 0
			end if
		else
			Object.ChangeY = 0
		end if
	end if

	Object.Pressed = false

	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		temp0 = Object.XPos
		Object.XPos = Object.PlatformX
		BoxCollisionTest(C_PLATFORM, object.entityPos, -32, -16, 32, 8, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
		if CheckResult == true
			Object.Pressed = true
			object[0].XPos += Object.ChangeX
			object[0].YPos += Object.ChangeY
		end if

		Object.XPos = temp0
	next
end event

event ObjectDraw

	Cos(Object.PlatformX, Object.Angle)
	Object.PlatformX <<= 13
	Object.PlatformX += Object.XPos
	Object.PlatformX &= 4294901760

	Object.YPos += Object.ChangeY
	DrawSpriteXY(0, Object.PlatformX, Object.YPos)
end event

event ObjectStartup
	LoadSpriteSheet("SSZ/objects.gif")
	//Platform Frame
	SpriteFrame(-35, -18, 70, 36, 148, 18)
end event

// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event

event RSDKStartup
	LoadSpriteSheet("SSZ/objects.gif")
	//Platform Frame
	SpriteFrame(-35, -18, 70, 36, 148, 18)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event