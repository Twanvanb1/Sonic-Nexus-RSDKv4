// ----------------------------------
// RSDK Project:          Sonic Nexus v4
// Script Description:    Chompy Object 
// Script Author:         Twanvanb1
// ----------------------------------


// ========================
// Aliases
// ========================

private alias Object.Value0 : Object.XVelocity
private alias Object.Value1 : Object.YVelocity

private alias Object.Value2 : Object.OldXPos
private alias Object.Value3 : Object.OldYPos
private alias Object.Value4 : Object.Timer

private alias 32 : TYPE_FISH

private alias 9 : ANI_SPINDASH
private alias 10 : ANI_JUMPING
private alias 15 : TYPE_EXPLOSION
private alias 20 : TYPE_INVINCIBLE

//----Player Movement States----//
private alias 8 : GETTINGHURT
private alias Object.Value4 : Player.Invincibility

// ========================
// Function Declarations
// ========================

reserve function Chompy_DebugDraw
reserve function Chompy_DebugSpawn

// ========================
// Function Definitions
// ========================

private function Chompy_DebugDraw
	DrawSprite(0)
end function


private function Chompy_DebugSpawn
	CreateTempObject(TypeName[Chompy], 0, object.xpos, object.ypos)
end function

// ========================
// Events
// ========================

event ObjectUpdate

	switch Object.State
	case 0
		Object.Timer++
		if Object.Timer == 1
			Object.Timer = 0
			Object.OldYPos = Object.YPos
			if Object.PropertyValue == 0
				Object.XVelocity =- 0x20000
				Object.Rotation = 512
			else
				Object.XVelocity = 0x20000
				Object.Rotation = 0
			end if
			Object.YVelocity =- 0x9C000
			Object.State = 1
			Object.Priority = 1
		end if
		break
	case 1
		Object.XPos += Object.XVelocity
		Object.YPos += Object.YVelocity

		Object.YVelocity += 0x3000

		if Object.PropertyValue == 0
			Object.Rotation -= 3
		else
			Object.Rotation += 3
		end if

		if Object.YVelocity > 0x9C000
			Object.Priority = 0
			Object.State = 0
			Object.YPos = Object.OldYPos
			if Object.PropertyValue == 0
				Object.PropertyValue = 2
			else
				Object.PropertyValue = 0
			end if
		end if
		break
	end switch

    foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, -14, -14, 14, 14, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
		if checkResult == true
			CallFunction(Player_BadnikBreak)
		end if
	next

end event

event ObjectDraw
	temp0 = Object.Frame
	temp0 >>= 3
	DrawSpriteFX(temp0, FX_ROTATE, Object.XPos, Object.YPos)
	Object.Frame++

	if Object.Frame > 15
		Object.Frame = 0
	end if
end event

event ObjectStartup
	LoadSpriteSheet("SSZ/Objects.gif")
	
	//Fish Frames
	SpriteFrame(-16, -16, 32, 32, 1, 91)
	SpriteFrame(-16, -16, 32, 31, 34, 91)

	SetTableValue(TypeName[Chompy], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(Chompy_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(Chompy_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event

// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("SSZ/Objects.gif")
	//Fish Frame
	SpriteFrame(-16, -16 ,32, 32, 1, 91)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event