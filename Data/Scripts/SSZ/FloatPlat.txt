// ----------------------------------
// RSDK Project: Sonic Nexus v4
// Script Description: Floating Platform
// Script Author: Twanvanb1
// ----------------------------------

#alias 26 : TYPE_FLOATPLAT

private alias Object.Value0 : Object.Pressed
private alias Object.Value1 : Object.PlatformY
private alias Object.Value2 : Object.ChangeY

event ObjectUpdate
	if Object.Pressed == true
		if Object.PlatformY < 16
			Object.PlatformY++
			temp0 = Object.PlatformY
			temp0 &= 3
			if temp0 == 3
				Object.ChangeY = 65536
			else
				Object.ChangeY = 0
			end if
		else
			Object.ChangeY = 0
		end if
	else
		if Object.PlatformY > 0
			Object.PlatformY--
			temp0 = Object.PlatformY
			temp0 &= 3
			if temp0 == 3
				Object.ChangeY = -65536
			else
				Object.ChangeY = 0
			end if
		else
			Object.ChangeY = 0
		end if
	end if

	Object.Pressed = false
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)	
		BoxCollisionTest(C_PLATFORM, object.entityPos, -32, -16, 32, 8, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
		if CheckResult == true
			Object.Pressed = true
			object[0].YPos += Object.ChangeY
		end if
	next
end event

event ObjectDraw
	Object.YPos += Object.ChangeY
	DrawSprite(0)
end event

event ObjectStartup
	LoadSpriteSheet("SSZ/Objects.gif")
	//Platform Frame
	SpriteFrame(-35, -18, 70, 36, 148, 18)
end event

// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event

event RSDKStartup
	LoadSpriteSheet("SSZ/Objects.gif")
	//Platform Frame
	SpriteFrame(-35, -18, 70, 36, 148, 18)
end event